---
page_title: "Opal Provider"
description: |-
  The Opal provider provides resources for managing Opal owners, resources, and groups.
---

# Opal Provider

The Opal provider provides resources for managing Opal owners, resources, and groups.

## Example Usage

```terraform
terraform {
  required_providers {
    opal = {
      source = "registry.terraform.io/opalsecurity/opal"
    }
  }
}

provider "opal" {
}

data "opal_app" "opal" {
  # App ids can be retrieved via the Opal web app or via the API (https://docs.opal.dev/reference/getapps)
  id = "dbe38d2d-9ce4-4d13-95a4-945716a257b4"
}

data "opal_app" "my_custom_app" {
  id = "03c06479-6ffa-45e1-9f65-cd470ff128b3"
}

data "opal_user" "alice" {
  email = "alice@mycompany.com"
}

data "opal_user" "bob" {
  id = "e5e5ba2b-e126-4699-a8bc-dc186d490b6e"
}

resource "opal_owner" "security" {
  name = "Security Team"

  user {
    id = data.opal_user.alice.id
  }

  user {
    id = data.opal_user.bob.id
  }
}

resource "opal_resource" "sensitive_resource" {
  name = "Sensitive Resource"
  description = "A sensitive resource that should be accessed for on-call only."
  resource_type = "CUSTOM"
  app_id = data.opal_app.my_custom_app.id
  admin_owner_id = opal_owner.security.id
  visibility = "LIMITED"

  visibility_group {
    id = opal_group.oncall.id
  }

  reviewer_stage {
    reviewer {
      id = opal_owner.security.id
    }
  }
}

resource "opal_group" "oncall" {
  name = "On-Call Rotation"
  group_type = "OPAL_GROUP"
  app_id = data.opal_app.opal.id
  require_mfa_to_approve = true
  admin_owner_id = opal_owner.security.id

  reviewer_stage {
    reviewer {
      id = opal_owner.security.id
    }
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `base_url` (String) The base Opal API url in the format `https://[hostname]`. The default value is `https://api.opal.dev`. The value must be provided when working with on-prem
- `token` (String, Sensitive) The authentication token used to connect to Opal. The value can be sourced OPAL_AUTH_TOKEN.
